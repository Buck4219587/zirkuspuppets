PUPPET Zirkusclown
  CHARSET: "UTF-8"
  LOGIN: " kommt zur Arbeit."
  LOGOUT: " hat Feierabend."
  APPEAR: " kommt um aufzuräumen."
  DISAPPEAR: " hat aufgeräumt."
  INFO: "Ich halte die Zirkusräumlichkeiten sauber."
  OVERFLOW: HARAKIRI
  NEWOWNER: NO
  CITYCHAT: YES
  CASESENSITIV: YES
#  SAVE: "save*"
  OWN: NO

@list: listebefehle
@start: rundgang
@clear: clear
@skipped: listskipped

ACTION start
  SET HOME C177
  SET GAMELIST "11nimmt 6nimmt 7Wonders Abluxxen AllesImEimer Atlantis Attika Attribut AufAchse Backgammon Bazaar Biberbande Blaetterrauschen Blockers Bluff Bohnanza BohnDuell Bruegge CafeInternational CantStop Carcassonne Cartagena CastlesOfTuscany Caylus CC-JaegerUndSammler Clans CleverHochDrei Clever4Ever CouncilOfShadows Crosswise Diamant DieCrew Discordia Dizzle Dog Doppelkopf DoppeltSoClever Drachenherz Dudeln DungeonsDiceDanger Dvonn Emerald Evolution Explorers FinstereFlure Fuersten Funkenschlag GanzSchoenClever Geister Go GoennenKoennen Grimoria Hadara Halali HalfPintHeroes Helvetia HeulDoch Imhotep Imperial ImSchutzeDerBurg Intrige Kardinal&Koenig KingdomBuilder KingLui Kingsburg KrassKariert Kribbeln LessIsMore Ligretto LionsOfLydia Manhattan Manitou MarcoPolo Medina Meuterer Milestones Monasterium MonsterExpedition NochMal NotreDame Numeri OhneFurchtUndAdel PackeisAmPol Pantheon ParisParis PiranhaPedro PuertoRico Saboteur SaeulenDerErde SanJuan SanktPetersburg SantaCruz Sauscharf Set Siedler SkullKing SpaceBob Sticheln SuperMegaLuckyBox StoneAge StreetSoccer ThurnUndTaxis Tichu Tongiaki Transamerica Tulpenfieber TurnIt UmKroneUndKragen Vabanque Venedig Verflixxt Verraeter VollVerplant Wikinger Wizard Wuerfelhelden Yinsh Zatre"
  # wird mit dem täglichen puppetstart neu befüllt
  SET ROOMSTOSKIP ""
  WHEN ERROR DO error
  WHEN TIMER 10800 DO rundgang
  WHEN KEYWORD "!list" IN TELL DO listebefehle
  WHEN KEYWORD "!start" IN TELL DO rundgang
  WHEN KEYWORD "!clear" IN TELL DO clear
  WHEN KEYWORD "!skipped" IN TELL DO listskipped
END

ACTION listebefehle
  -- @start Starte den Aufräumvorgang
  -- @clear Setze die Liste der übersprungenen Räume zurück
  -- @skipped Listet die übersprungenen Räume auf
END

ACTION clear
  SET ROOMSTOSKIP ""
END

ACTION listskipped
  -- "Übersprunngen: " [ROOMSTOSKIP]
END

# counter: Zähler für die While-Schleife
# players: Summiert die Anzahl der gefundenen Spieler in den Räumen
# resets: Anzahl der Räume, in denen ein Reset durchgeführt wurde
ACTION rundgang
  SET counter 0
  SET players 0
  SET resets 0
  SET missinggames [GAMELIST]
  # Fix: Biberbande gibt es nicht mehr, nur wegen SMLB in der Liste
  REMOVELIST missinggames "Biberbande"

  SET CSS "div.segment{padding:4px;}div.propmenu{font-size:12;font-family:ui-monospace;background-color:ghostwhite;}button.prop{font-size:18px;background-color:ff6666;padding:10px 28px;border-radius:0px;border:none;color:white;margin-left:5px;margin-right:5px}h1{font-family:arial,sans-serif;font-size:20px;font-weight:bold;text-align:center}table.zirkusTable{border:2px solid #a40808;background-color:#fff;width:100%;text-align:left;border-collapse:collapse}table.zirkusTable td,table.zirkusTable th{border:1px solid #aaa;padding:4px 1px}table.zirkusTable tbody td{font-size:13px}table.zirkusTable tr:nth-child(even){background:#f5c8bf}table.zirkusTable thead{background:#a40808}table.zirkusTable thead th{font-size:16px;font-weight:700;color:#fff;text-align:center;border-left:2px solid #a40808}table.zirkusTable thead th:first-child{border-left:none}"
  SET SCRIPT "function menustring(e,n){var r='# Menu \['+e+'\]\\n',t=Array.from(n.keys()).sort();for(game of t)for(room of n.get(game))r+=e+'-'+game.replace('-','')+' = */game '+room+'\\n';return r}function genmenu(){let e=\['Dislodge','FiveDice','Mahjongg','MogelPat','Sologammon'\];var n=document.getElementById('raumliste'),r=document.getElementById('propmenu'),t=new Map,i=new Map,s=new Map,o=new Map;for(let u of n.rows){var l=u.cells\[4\].innerText,a=u.cells\[0\].innerText,g=u.cells\[2\].innerText;if('TRUE'===l&&g.length>0){var m=g.substring(0,1).toLowerCase();if(e.includes(g))var f=o;else if(m<='d')var f=t;else if(m<='o')var f=i;else if(m<='z')var f=s;f.has(g)||f.set(g,new Set),f.get(g).add(a)}}r.innerText=menustring('MenuZirkusMU_A_D',t)+'\\n'+menustring('MenuZirkusMU_E_O',i)+'\\n'+menustring('MenuZirkusMU_P_Z',s)+'\\n'+menustring('MenuZirkusSU',o)+'\\n'}function clearmenu(){document.getElementById('propmenu').innerText=''}"

  SET PUBLIC1 "<html><head><meta charset='utf-8'/><style>[CSS]</style></head><body><script>[SCRIPT]</script><h1>Raumliste Zirkus</h1><div class='segment'><button class='prop' onclick='genmenu()'>Prop Men&uuml; erstellen</button><button class='prop' onclick='clearmenu()'>Leeren</button></div><div class='propmenu' id='propmenu'></div>"
  SET PUBLIC2 "<div class='segment'><table class='zirkusTable' id='raumliste'><tbody><tr><th>Raum</th><th>Name</th><th>Spiel</th><th>Besitzer</th><th>Aktiv</th></tr>"
  SET publiccnt 3
  SET PUBLIC[publiccnt] ""
  
  -- Fange an aufzuräumen
  
  WHILE [counter] <= 200
    BEGIN
      SET zielraum "C177-[counter]"
      IF NOT [zielraum] INLIST [ROOMSTOSKIP]
        BEGIN
          >> /room [zielraum]              # Wechsel in den nächsten Raum
          GETROOMINFO
          IF [ROOM] == [zielraum]
            BEGIN
              # -- Ich bin jetzt in Raum [ROOM] mit Spiel [GAME]
              IF LISTLENGTH [GAME] > 1
                BEGIN
                  DO subraeume
                END
              ELSE
                BEGIN
                  SET spiel [GAME]
                  # Fix: SMLB taucht als Biberbande auf
                  IF [spiel] == "Biberbande"
                    BEGIN
                      SET spiel "SuperMegaLuckyBox"
                    END
                  SET raum [ROOM]
                  DO raumreset
                  DO roominfotopublic
                END
            END
          ELSE # Raum existiert nicht
            BEGIN
              # -- [zielraum] existiert nicht
              ADDLIST ROOMSTOSKIP "[zielraum]"
            END
        END
      ELSE
        BEGIN
          # -- [zielraum] wurde übersprungen
        END
      EVAL counter = [counter] + 1
      SLEEP 3
    END  # END WHILE
  
  >> /room [HOME]
  >> /ctell [resets] Räume zurückgesetzt. 
  -- Im Zirkus spielen aktuell [players] Personen. [resets] Räume zurückgesetzt.
  
  EVAL publiccnt = [publiccnt] + 1
  SET PUBLIC[publiccnt] "</tbody></table></div>"
  
  DO missinggamestopublic

  GETDATE
  IF [HOUR] < 10
    SET HOUR "0[HOUR]"
  IF [MIN] < 10
    SET MIN "0[MIN]"

  EVAL publiccnt = [publiccnt] + 1
  SET PUBLIC[publiccnt] "<div class='segment'>Last run: [DAY].[MONTH] [HOUR]:[MIN]</div></body></html>"
  
  WHILE [publiccnt] <= 100
    BEGIN
      EVAL publiccnt = [publiccnt] + 1
      UNSET PUBLIC[publiccnt]
    END

  SAVEPUBLIC

END

# Iteriert durch die Subräume eines Gebäudes mit mehreren Spielen
# GAME, GAMELIST, players, reset müssen gesetzt sein
ACTION subraeume
  SET sumraum 1
  FOR spiel IN [GAME]
    BEGIN
      # Fix: SMLB taucht als Biberbande auf
      IF [spiel] == "Biberbande"
        BEGIN
          SET spiel "SuperMegaLuckyBox"
        END
      SET raum "[zielraum]-[sumraum]"
      # -- Spiel [spiel] in Raum [raum]
      >> /room [raum]
      DO raumreset
      DO roominfotopublic
      EVAL sumraum = [sumraum] + 1
    END
END

# Für Spielräume: setzt zurück oder zählt Anzahl spieler
# IN GAMELIST: Liste der Spiele
# IN spiel: Spiel im aktuellen Raum
# IN players: Counter für die Anzahl der Spieler 
# IN resets: Counter für die Anzahl der zurückgesetzten Räume 
# IN/OUT missinggames: Liste von Spielen, ein gefundenes Spiel wird aus der Liste entfernt
ACTION raumreset
  IF [spiel] INLIST [GAMELIST]
    BEGIN
      REMOVELIST missinggames [spiel]
      DO spielerzahl
      IF [x] > 0
        BEGIN
          # -- Hier sind die folgenden Personen [WHO]
          EVAL players = [players] + [x]
        END
      ELSE
        BEGIN
          # -- Hier ist niemand... führe reset durch.
          >> /reset
          EVAL resets = [resets] + 1
        END
      END
  ELSE # Spiel nicht in Liste
    BEGIN
      # -- [spiel] ist nicht in der Liste der Spiele.
    END
END

ACTION spielerzahl
  GETWHO                               # liefert in [WHO] eine Liste mit
                                       # den Namen der Anwesenden
  SET x 0                              # die Anzahl x wird zu Beginn auf 0 gesetzt.
  FOR anwesender IN [WHO]              # Alle Namen aus der Liste
                                       #   werden abgearbeitet.
    BEGIN
      GETINFO [anwesender]             # Die Infos zum Namen werden gelesen.
      IF NOT [PUPPET]                  # Puppets werden nicht gezählt.
        EVAL x = [x] + 1               # Für jeden Spieler wird x erhöht.
    END
END

# GETROOMINFO muss zuvor aufgerufen werden
# publiccnt muss gesetzt sein
ACTION roominfotopublic
  SET arg1 [NAME]
  DO htmlEncode
  LOCAL raumname = [out1]

  SET arg1 [OWNER]
  DO htmlEncode
  LOCAL raumowner = [out1]

  SET arg1 "<tr><td>[raum]</td><td>[raumname]</td><td>[spiel]</td><td>[raumowner]</td><td>[ACTIVE]</td></tr>"
  DO writeToPublic
END

# Schreibt missinggames in eine Tabelle
# publiccnt muss gesetzt sein
ACTION missinggamestopublic
  EVAL publiccnt = [publiccnt] + 1
  SET PUBLIC[publiccnt] "<div class='segment'><table class='zirkusTable'><tbody><tr><th>Fehlendes Spiel</th></tr>"

  FOR missing IN [missinggames]
    BEGIN
      SET arg1 "<tr><td>[missing]</td></tr>"
      DO writeToPublic
    END

  EVAL publiccnt = [publiccnt] + 1
  SET PUBLIC[publiccnt] "</tbody></table></div>"
END

# Schreibt text in die public-Variablen
# IN/OUT publiccnt der aktuelle public-Zähler
# IN arg1: Der zu schreibende Text
ACTION writeToPublic
  LOCAL currenttext = [arg1]
  EVAL publength = LENGTH [PUBLIC[publiccnt]]
  EVAL currenttextlength = LENGTH [currenttext]
  EVAL totallength = [publength] + [currenttextlength]

  IF [totallength] > 4500
    BEGIN
      EVAL publiccnt = [publiccnt] + 1
      SET PUBLIC[publiccnt] ""
    END

  SET PUBLIC[publiccnt] "[PUBLIC[publiccnt]] [currenttext]"
END

# Ersetzt in einem String Sonderzeichen duch HTML-Encodierte Zeichen
# IN arg1: Der zu bearbeitende String
# OUT out1: Der bearbeitete String
ACTION htmlEncode
  LOCAL tmp = [arg1]
  REPLACE tmp "ä" "&auml;"
  REPLACE tmp "ö" "&ouml;"
  REPLACE tmp "ü" "&uuml;"
  REPLACE tmp "Ä" "&Auml;"
  REPLACE tmp "Ö" "&Ouml;"
  REPLACE tmp "Ü" "&Uuml;"
  REPLACE tmp "ß" "&szlig;"  
  REPLACE tmp "é" "&eacute;"
  REPLACE tmp "É" "&Eacute;"
  REPLACE tmp "è" "&egrave;"
  REPLACE tmp "È" "&Egrave;"
  SET out1 [tmp]
END

ACTION error
  >> Ein Fehler ist aufgetreten ([MESSAGE])
END

PUPPETEND
